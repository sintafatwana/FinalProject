plugins {
    id 'java'
    id 'io.qameta.allure' version '2.11.2'
}

group = 'org.example'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    // Selenium Java
    implementation 'org.seleniumhq.selenium:selenium-java:4.21.0'
    // WebDriverManager
    implementation 'io.github.bonigarcia:webdrivermanager:5.8.0'
    // JUnit 4
    testImplementation 'junit:junit:4.13.2'
    // Cucumber
    testImplementation 'io.cucumber:cucumber-java:7.8.1'
    testImplementation 'io.cucumber:cucumber-junit:7.8.1'
    // Rest Assured
    testImplementation 'io.rest-assured:rest-assured:5.4.0'
    testImplementation 'io.rest-assured:json-schema-validator:5.4.0'
    implementation 'org.json:json:20240303'
    // Allure untuk JUnit 4
    testImplementation 'io.qameta.allure:allure-junit4:2.24.0'
    // Kalau pakai Cucumber, tambahkan juga:
    testImplementation 'io.qameta.allure:allure-cucumber7-jvm:2.24.0'
}

test {
    useJUnit() // JUnit 4 pakai ini
    testLogging {
        events "passed", "skipped", "failed"
    }
}
allure {
    version = '2.24.0'
}

configurations {
    cucumberRuntime {
        extendsFrom testImplementation
    }
}


def tags = (findProperty('tags') == null) ? 'not @exclude' : "${findProperty('tags')} and not @exclude"

tasks.register('webCucumber', JavaExec) {
    description = "Run Web UI Cucumber Tests"
    group = "verification"
    ignoreExitValue = true

    classpath = configurations.cucumberRuntime +
            sourceSets.main.output +
            sourceSets.test.output

    mainClass.set("io.cucumber.core.cli.Main")

    args = [
            '--plugin', 'pretty',
            '--plugin', 'html:reports/web-report.html',
            '--plugin', 'json:reports/web-report.json',
            '--glue', 'tests.stepdefs',
            '--tags', '@web and not @exclude',
            'src/test/resources'
    ]

    dependsOn 'assemble', 'compileTestJava'
}

tasks.register('apiCucumber', JavaExec) {
    description = "Run API Cucumber Tests"
    group = "verification"
    ignoreExitValue = true

    classpath = configurations.cucumberRuntime +
            sourceSets.main.output +
            sourceSets.test.output

    mainClass.set("io.cucumber.core.cli.Main")

    args = [
            '--plugin', 'pretty',
            '--plugin', 'summary',
            '--plugin', 'html:reports/api-report.html',
            '--plugin', 'json:reports/api-report.json',
            '--plugin', 'io.qameta.allure.cucumber7jvm.AllureCucumber7Jvm',
            '--glue', 'tests.stepdefs',
            '--tags', '@api and not @exclude',
            'src/test/resources'
    ]

    dependsOn 'assemble', 'compileTestJava'
}
